INCLUDE_DIRECTORIES (${AZOTH_INCLUDE_DIR}
	${CMAKE_CURRENT_BINARY_DIR}
	)
SET (AUTOIDLER_SRCS
	autoidler.cpp
	xmlsettingsmanager.cpp
	3dparty/idle.cpp
	)
SET (AUTOIDLER_HEADERS
	autoidler.h
	xmlsettingsmanager.h
	3dparty/idle.h
	)
SET (AUTOIDLER_RESOURCES azothautoidlerresources.qrc)
QT4_ADD_RESOURCES (AUTOIDLER_RCCS ${AUTOIDLER_RESOURCES})

IF (APPLE)
	SET (AUTOIDLER_SRCS ${AUTOIDLER_SRCS} 3dparty/idle_mac.cpp)
ELSEIF (UNIX)
	SET (AUTOIDLER_SRCS ${AUTOIDLER_SRCS} 3dparty/idle_x11.cpp)

	FIND_PACKAGE (X11)

	IF (X11_Xscreensaver_FOUND)
		ADD_DEFINITIONS (-DHAVE_XSS)
		INCLUDE_DIRECTORIES ("${X11_Xscreensaver_INCLUDE_PATH}")
		SET (ADDITIONAL_AUTOIDLER_LIBS ${X11_Xscreensaver_LIB})
	ENDIF (X11_Xscreensaver_FOUND)
ELSEIF (WIN32)
	SET (AUTOIDLER_SRCS ${AUTOIDLER_SRCS} 3dparty/idle_win.cpp)
ENDIF (APPLE)

ADD_DEFINITIONS (-DUNICODE)

CreateTrVars ("azoth_autoidler" "en;es;ru_RU;uk_UA" AUTOIDLER_TRANSLATIONS AUTOIDLER_COMPILED_TRANSLATIONS)
IF (NOT LC_NO_MOC)
	QT4_WRAP_CPP (AUTOIDLER_MOC_SRCS ${AUTOIDLER_HEADERS})
ENDIF (NOT LC_NO_MOC)

ADD_CUSTOM_COMMAND (OUTPUT
	${AUTOIDLER_COMPILED_TRANSLATIONS}
	COMMAND "${QT_LRELEASE_EXECUTABLE}" ${AUTOIDLER_TRANSLATIONS}
	DEPENDS ${AUTOIDLER_TRANSLATIONS}
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	)
ADD_LIBRARY (leechcraft_azoth_autoidler SHARED
	${AUTOIDLER_COMPILED_TRANSLATIONS}
	${AUTOIDLER_SRCS}
	${AUTOIDLER_MOC_SRCS}
	${AUTOIDLER_RCCS}
	)
TARGET_LINK_LIBRARIES (leechcraft_azoth_autoidler
	${QT_LIBRARIES}
	${LEECHCRAFT_LIBRARIES}
	${ADDITIONAL_AUTOIDLER_LIBS}
	)
INSTALL (TARGETS leechcraft_azoth_autoidler DESTINATION ${LC_PLUGINS_DEST})
INSTALL (FILES azothautoidlersettings.xml DESTINATION ${LC_SETTINGS_DEST})
INSTALL (FILES ${AUTOIDLER_COMPILED_TRANSLATIONS} DESTINATION ${LC_TRANSLATIONS_DEST})
